apply plugin: 'maven-publish'
apply plugin: 'signing'

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

apply from: "${rootProject.projectDir}/scripts/sonatype.gradle"

publishing {
    publications {
        release(MavenPublication) {
            groupId PUBLISH_GROUP_ID
            artifactId PUBLISH_ARTIFACT_ID
            version PUBLISH_VERSION

            if (project.plugins.findPlugin("com.android.library")) {
                artifact("$buildDir/outputs/aar/${project.getName()}-release.aar")
            } else {
                artifact("$buildDir/libs/${project.getName()}-${version}.jar")
            }

            pom {
                name = PUBLISH_ARTIFACT_ID
                description = 'Sprig official Segment Destination'
                url = 'https://docs.sprig.com/installation/android-sdk'
                licenses {
                    license {
                        name = 'Userleap License'
                        url = 'https://sprig.com/terms-conditions.html'
                    }
                }
                // Version control info - if you're using GitHub, follow the format as seen here
                scm {
                    connection = 'scm:git@github.com:github.com/UserLeap/analytics-kotlin-sprig.git'
                    developerConnection = 'scm:git:ssh://github.com/UserLeap/analytics-kotlin-sprig.git'
                    url = 'https://github.com/UserLeap/analytics-kotlin-sprig/tree/main'
                }
                developers {
                    developer {
                        id = 'gongster'
                        name = 'Gong Chen'
                        email = 'gong@sprig.com'
                    }
                }
                withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')

                    project.configurations.implementation.allDependencies.each {
                        if (it.name != 'unspecified') {
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', it.group)
                            dependencyNode.appendNode('artifactId', it.name)
                            dependencyNode.appendNode('version', it.version)
                        }
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = "sonatype"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"

            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }
}

nexusPublishing {
    packageGroup = PUBLISH_GROUP_ID
    repositories {
        sonatype {
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))

            stagingProfileId = sonatypeStagingProfileId

            username = ossrhUsername
            password = ossrhPassword
        }
    }
}

signing {
    sign publishing.publications
}